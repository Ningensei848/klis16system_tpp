% [定理 31] ----------------------------------------------------------;
% 内接円の中心Iを原点(0, 0)と一致するようにしておく
% I(0, 0) A(u1, u2) B(u3, u4) C(u5, u6)

%----------------------------------------------------------------------;

% 関数定義読み込み） ----------------------------------------------------;

load_package groebner;
on comp,gcd,ezgcd;
off allfac,pwrds;
in cal_sys_relations$


% ２点間のユークリッド距離D^2 -----------------------------------------------;
procedure squared_euclid(a1,a2,b1,b2)$
begin
 scalar d;
 d:=(a1-b1)^2+(a2-b2)^2;
 return d
end$
%----------------------------------------------------------------------;

% ---------------------------------------------------------------------;
% ＜証明＞ -------------------------------------------------------------;

order x16, x15, x14, x13, x12, x11, x10, x09, x08, x07, x06, x05, x04, x03, x02, x01, u6, u5, u4, u3, u2, u1, u0;
factor x16, x15, x14, x13, x12, x11, x10, x09, x08, x07, x06, x05, x04, x03, x02, x01;

% ---------------------------------------------------------------------;
% 仮定 ----------------------------------------------------------------;

% △ABCの内接円Iが辺BCに接する点をXとする => X(x01, x02)
% IX⊥BC かつ B-X-C (共線となっている)

h1:=vertically(0, 0, x01, x02, u3, u4, u5, u6);
h2:=collinear(u3, u4, x01, x02, u5, u6);


% 辺BC, CAの中点をL,Mとする => L(x03, x04) M(x05, x06)

h3:=midpoint(1, u3, u4, x03, x04, u5, u6);
h4:=midpoint(0, u3, u4, x03, x04, u5, u6);
h5:=midpoint(1, u5, u6, x05, x06, u1, u2);
h6:=midpoint(0, u5, u6, x05, x06, u1, u2);


% 点AからBCに下ろした垂線の足をDとする => D(x07, x08)
% AD⊥BC かつ B-D-C (共線となっている)

h7:=vertically(u1, u2, x07, x08, u3, u4, u5, u6);
h8:=collinear(u3, u4, x07, x08, u5, u6);


% AIとBCの交点をKとすると、AKは∠Aの二等分線である => K(x09, u0)
% すなわち、 ∠BAK=∠CAK
% A-I-K

h9:=collinear(u1, u2, 0, 0, x09, u0);


% 点Kから内接円に接線を引き、その接点をTとし、 => T(0, u0)
% 接線KTと辺CA上との交点をFとする => F(x10, u0)
% C-F-A (共線となっている)

h10:=collinear(u5, u6, x10, u0, u1, u2);


% BF, AK の交点をEとする => E(x11, x12)
% A-E-K => A-I-E

h11:=collinear(u1, u2, 0, 0, x11, x12);


% ∠BAK=∠FAK, ∠BKA=∠FKAから、 一組の辺とその両端の角が等しいので、△BAK≡△FAK
% ∴ AB=AF, ∠AFK=∠ABC
% よって、 AE⊥BF, BF=FEである
% AE⊥BF => AI⊥BE

h12:=vertically(u1, u2, 0, 0, u3, u4, x11, x12);


% したがって、 2*EL = FC = AC-AF = AC-AB かつ EL // FC である

% また、AC-AB = CX-BX = (CL+XL) - (BL-XL) = 2*XL
% ∴ EL = XL … ①
% ∠ADB=∠AEB=∠R から、点D,EはABを直径とする円周上にあるから、
% ∠EDL = ∠BAE = ∠KAC = ∠KEL ( ∵ EL//AC )
% ∴ EL^2 = DL・KL … ②
% ①, ②から XL^2 = DL・KL
% LTが再び内接円Iと交わる点をSとすると => S(x13, x14)

% L-T-S (共線となっている)

h13:=collinear(x03, x04, 0, u0, x13, x14);


% また、XL・XL=TL・SLであるから（方べきの定理の逆）
% DL・KL=TL・SL <=> (DL・KL)^2 - (TL・SL)^2 = 0

dl2:=squared_euclid(x07, x08, x03, x04);
kl2:=squared_euclid(x09, u0, x03, x04);
tl2:=squared_euclid(0, u0, x03, x04);
sl2:=squared_euclid(x13, x14, x03, x04);

h14:=(dl2*kl2)-(tl2*sl2);


% よって、4点D, K, T, S は同一円周上にある ( ∵ 方べきの定理の逆 )
% ∴ ∠DML=  ∠TKL = ∠FKC = ∠AFK-∠C = ∠B-∠C … ③

% ∠ADC=∠R で点MはACの中点であるから、MD=MC
% また、ML//AB (∵ 中点連結定理)
% ∴ ∠DMC = ∠MLC - ∠MDC = ∠B-∠C … ④
% ③, ④から、 ∠DSL=∠DML
% よって4点S, D, L, M は同一円周上にある, すなわち点Sは9点円DLM(§２例２参照[教科書 p.5])上にある

% つまり、SL, DMの交点をJとしたとき、 => J(x15, x16)
% 方べきの定理より、 SJ・JL=DJ・JMである
% SJ・JL=DJ・JM <=> (SJ・JL)^2 - (DJ・JM)^2 = 0

% このとき、DJ=MJとなるように点Jを取ると、必ず円SDLM内にJが存在する
% DJ=MJ かつ  S-J-L (=> L-T-J)

h15:=requal(x07, x08, x15, x16, x05, x06, x15, x16);
h16:=collinear(x03, x04, 0, u0, x15, x16);

sj2:=squared_euclid(x13, x14, x15, x16);
jl2:=squared_euclid(x15, x16, x03, x04);
dj2:=squared_euclid(x07, x08, x15, x16);
jm2:=squared_euclid(x15, x16, x05, x06);

%h17:=(sj2*jl2)-(dj2*jm2);


% ---------------------------------------------------------------------;
% 結論 ----------------------------------------------------------------;

% 点Sにおいて、内接円Iに接線SG (Gは任意の点) を引くと、
% ∠GSL = ∠GST = ∠STF = ∠SDL
% であるから、SGは円SDLに接する
% ∠STF = ∠SDL

conclusion:=tangent(x13, x14, 0, u0, x10, u0, x13, x14, x07, x08, x03, x04);


% つまり、SGは内接円Iと９点円の共有点Sにおける共有接線である
% よって、内接円と9点円は点Sはで接する
% ( ※同様にして、傍接円Jに接することも証明できるらしいが、時間が省略 )


%----------------------------------------------------------------------;
% Groebner Basis: 結果が1となったら、仮定が誤っている可能性が高い ---------------;

%%% 変数を定義し、lex形式で並べる ------------------------------------------;
torder({x16, x15, x14, x13, x12, x11, x10, x09, x08, x07, x06, x05, x04, x03, x02, x01}, lex)$



%%% 仮定において定義した式からGroebner Basisを求める -------------------------;
gb:=groebner{h1, h2, h3, h4, h5, h6, h7, h8, h9, h10, h11, h12, h13, h14, h15, x16};



%---------------------------------------------------------;
% 「glterms」が出力するのは、グレブナー基底の計算過程で〈ゼロにはならない〉
% と仮定された式のリストである。
%---------------------------------------------------------;

%%% u に関する制約条件 --------------------------------------------------;
glterms;

%%% gbを法としてgを簡約 --------------------------------------------------;
preduce(conclusion, gb);

% ==> 0 になっていれば、定理は成立 ------------------------------------------;


showtime;

;end;
