% [定理 31] ----------------------------------------------------------;
% 内接円の中心Iを原点(0, 0)と一致するようにしておく
% I(0, 0) A(u1, u2) B(u3, u4) C(u5, u6)
% X(x1, y1) D(x2, y2) E(x3, y3) F(x4, y4)
% K(x5, y5) L(x6, y6) M(x7, y7) S(x8, y8) T(x9, y9)
%----------------------------------------------------------------------;

% 関数定義読み込み） ----------------------------------------------------;

in cal_sys_relations$
on groebopt;

% ---------------------------------------------------------------------;
% ＜証明＞ -------------------------------------------------------------;

order x9, y9, x8, y8, x7, y7, x6, y6, x5, y5, x4, y4, x3, y3, x2, y2, x1, y1, u6, u5, u4, u3, u2, u1;
factor x9, y9, x8, y8, x7, y7, x6, y6, x5, y5, x4, y4, x3, y3, x2, y2, x1, y1;

% ---------------------------------------------------------------------;
% 仮定 ----------------------------------------------------------------;

% △ABCの内接円Iが辺BCに接する点をXとする
% IX⊥BC かつ B-X-C (共線となっている)

h1:=vertically(0, 0, x1, y1, u3, u4, u5, u6);
h2:=collinear(u3, u4, x1, y1, u5, u6);


% 辺BC, CAの中点をL,Mとする

h3:=midpoint(1, u3, u4, x6, y6, u5, u6);
h4:=midpoint(0, u3, u4, x6, y6, u5, u6);
h5:=midpoint(1, u5, u6, x7, y7, u1, u2);
h6:=midpoint(0, u5, u6, x7, y7, u1, u2);


% 点AからBCに下ろした垂線の足をDとする
% AD⊥BC かつ B-D-C (共線となっている)

h7:=vertically(u1, u2, x2, y2, u3, u4, u5, u6);
h8:=collinear(u3, u4, x2, y2, u5, u6);


% AIとBCの交点をKとすると、AKは∠Aの二等分線である
% A-I-K, B-K-C (共線となっている)

h9:=collinear(u1, u2, 0, 0, x5, y5);
h10:=collinear(u3, u4, x5, y5, u5, u6);


% 点Kから内接円に接線を引き、その接点をTとし、接線KTと辺CA上との交点をFとする
% K-T-F かつ C-F-A (共線となっている)

h11:=collinear(x5, y5, x9, y9, x4, y4);
h12:=collinear(u5, u6, x4, y4, u1, u2);


% ( 円の接線と半径は垂直に交わるから KF⊥IT である)

h13:=vertically(x5, y5, x4, y4, 0, 0, x1, y1);


% BF, AK の交点をEとする
% A-E-K, B-E-F (共線となっている)

h14:=collinear(u1, u2, x3, y3, x5, y5);
h15:=collinear(u3, u4, x3, y3, x4, y4);


% ∠BAK=∠FAK, ∠BKA=∠FKAから、 一組の辺とその両端の角が等しいので、△BAK≡△FAK
% ∴ AB=AF, ∠AFK=∠ABC
% よって、 AE⊥BF, BF=FEである
% したがって、 2*EL = FC = AC-AF = AC-AB

% かつ EL // FC である

h16:=parallel(x3, y3, x6, y6, x5, y5, u5, u6);


% また、AC-AB = CX-BX = (CL+XL) - (BL-XL) = 2*XL
% ∴ EL = XL … ①
% ∠ADB=∠AEB=∠R から、点D,EはABを直径とする円周上にあるから、
% ∠EDL = ∠BAE = ∠KAC = ∠KEL ( ∵ EL//AC )
% ∴ EL^2 = DL・KL … ②
% ①, ②から XL^2 = DL・KL
% LTが再び内接円Iと交わる点をSとすると
% L-T-S (共線となっている)

h17:=collinear(x6, y6, x9, y9, x8, y8);


% ( また、Sは内接円Iの円周上の点だから IS=IT=IX)

h18:=requal(0, 0, x8, y8, 0, 0, x1, y1);


% また、XL^2=TL・SLであることから DL・KL=TL・SL
% よって、4点D, K, T, S は同一円周上にある ( ∵ 方べきの定理の逆 )
% ∴ ∠DML=  ∠TKL = ∠FKC = ∠AFK-∠C = ∠B-∠C … ③

% ∠ADC=∠R で点MはACの中点であるから、MD=MC
% また、ML//AB (∵ 中点連結定理)
% ∴ ∠DMC = ∠MLC - ∠MDC = ∠B-∠C … ④
% ③, ④から、 ∠DSL=∠DML
% よって4点S, D, L, M は同一円周上にある, すなわち点Sは9点円DLM(§２例２参照[教科書 p.5])上にある

% 点Sにおいて、内接円Iに接線SG (Gは任意の点) を引くと、
% ∠GSL = ∠GST = ∠STF = ∠SDL
% であるから、SGは円SDLに接する

% ---------------------------------------------------------------------;
% 結論 ----------------------------------------------------------------;

% ∠STF = ∠SDL

conclusion:=tangent(x8, y8, x9, y9, x4, y4, x8, y8, x2, y2, x6, y6);


% つまり、SGは内接円Iと９点円の共有点Sにおける共有接線である
% よって、内接円と9点円は点Sはで接する
% ( ※同様にして、傍接円Jに接することも証明できるらしいが、時間が省略 )


%----------------------------------------------------------------------;
% Groebner Basis: 結果が1となったら、仮定が誤っている可能性が高い ---------------;

%%% 変数を定義し、lex形式で並べる ------------------------------------------;
torder({x9, y9, x8, y8, x7, y7, x6, y6, x5, y5, x4, y4, x3, y3, x2, y2, x1, y1}, revgradlex)$



%%% 仮定において定義した式からGroebner Basisを求める -------------------------;
gb:=groebner{h1, h2, h3, h4, h5, h6, h7, h8, h9, h10, h11, h12, h13, h14, h15, h16, h17, h18};



%---------------------------------------------------------;
% 「glterms」が出力するのは、グレブナー基底の計算過程で〈ゼロにはならない〉
% と仮定された式のリストである。
%---------------------------------------------------------;

%%% u に関する制約条件 --------------------------------------------------;
glterms;

%%% gbを法としてgを簡約 --------------------------------------------------;
preduce(conclusion, gb);

% ==> 0 になっていれば、定理は成立 ------------------------------------------;


showtime;

;end;
