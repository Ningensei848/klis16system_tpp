% [定理 １３] ----------------------------------------------------------;
% 〈メネラウス(Menelaus)の定理の証明〉
% △ABCの3辺BC, CA, ABまたはその延長が頂点を通らない１直線と交わる点をそれぞれ
% P, Q, R とする。 ※１直線P-Q-R が x軸と一致するように座標を取った
% A(u1, u2) B(u3, u4) C(u5, u6)
% P(x1, 0) Q(x2, 0) R(x3, 0)
%----------------------------------------------------------------------;

% ---------------------------------------------------------------------;
% ＜証明＞ -------------------------------------------------------------;

order x3, x2, x1, u6, u5, u4, u3, u2, u1;
factor x3, x2, x1;

% 関数定義読み込み（※ローカル環境へコピーしてきたもの） --------------------------;
in cal_sys_relations$


%----------------------------------------------------------------------;
% 仮定 ----------------------------------------------------------------;

% P, Q, Rは共線となっている -----------------------------------------------;

h1:=collinear(x1, 0, x2, 0, x3, 0);


% 各頂点からPQRに下ろした垂線の足をそれぞれD, E, Fとする
% D(u1, 0) E(u3, 0) F(u5, 0)
% つまり、PQ⊥BE, QR⊥CF, RP⊥ADとなる

h2:=vertically(x1, 0, x2, 0, u3, u4, u3, 0);
h3:=vertically(x2, 0, x3, 0, u5, u6, u5, 0);
h4:=vertically(x3, 0, x1, 0, u1, u2, u1, 0);


% A-B-P, B-Q-C, C-R-Aはそれぞれ共線となっている -----------------------------;

h5:=collinear(u1, u2, u3, u4, x1, 0);
h6:=collinear(u3, u4, x2, 0, u5, u6);
h7:=collinear(u5, u6, x3, 0, u1, u2);


%----------------------------------------------------------------------;
% 結論 ----------------------------------------------------------------;
% (BP*CQ*AR)^2 = (PC*QA*RB)^2 が導ければよい
% ※各辺の長さは負の数を取りえないので, 負の場合を考える必要はない

bp2:=(x1-u3)^2+(0-u4)^2;
cq2:=(x2-u5)^2+(0-u6)^2;
ar2:=(x3-u1)^2+(0-u2)^2;

pc2:=(u5-x1)^2+(u6-0)^2;
qa2:=(u1-x2)^2+(u2-0)^2;
rb2:=(u3-x3)^2+(u4-0)^2;

conclusion:=(bp2*cq2*ar2)-(pc2*qa2*rb2);

showtime;


%----------------------------------------------------------------------;
% Groebner Basis: 結果が1となったら、仮定が誤っている可能性が高い ---------------;

%%% 変数を定義し、lex形式で並べる ------------------------------------------;
torder({x3, x2, x1}, lex)$

%%% 仮定において定義した式からGroebner Basisを求める -------------------------;
gb:=groebner{h1, h2, h3, h4, h5, h6, h7};

%---------------------------------------------------------;
% 「glterms」が出力するのは、グレブナー基底の計算過程で〈ゼロにはならない〉
% と仮定された式のリストである。
%---------------------------------------------------------;

%%% u に関する制約条件 --------------------------------------------------;
glterms;


%%% gbを法としてgを簡約 --------------------------------------------------;
preduce(conclusion, gb);

% ==> 0 になっていれば、定理は成立 ------------------------------------------;

showtime;

;end;