%----------------------------------------------------------------------;
% 〈三角形の内心の証明〉
% A(0, 0) B(u1, 0) C(u2, u3) の三点による三角形ABCを考える
% 内心を作図から求める手順に沿って証明してみる
%----------------------------------------------------------------------;

% ---------------------------------------------------------------------;
% ＜証明＞ -------------------------------------------------------------;

in cal_sys_relations_kai;
order x11, x10, x9, x8, x7, x6, x5,x4, x3, x2, x1, u3, u2, u1;
factor x11, x10, x9, x8, x7, x6, x5,x4, x3, x2, x1;
tangent(x8, x7, x6, x5,x4, x3, x2, x1, u3, u2, u1, 0);

% 関数定義読み込み（※ローカル環境へコピーしてきたもの） --------------------------;
in cal_sys_relations$


%----------------------------------------------------------------------;
% 仮定 ----------------------------------------------------------------;


% ---------------------------------------------------------------------;
% 直線CA上に、AB=AD となる点Dをとる -----------------------------------------;
% BDの中点をEとすると、△ABDは二等辺三角形だから、AE⊥BDとなる --------------------;
% DはCA上にあるのだから、共線となっている ( C-D-Aと考える ) -----------------------;
% また、EはBD上にあり、共線となっている ( B-E-Dと考える ) -------------------------;
% D(x1, x2) E(x3, x4)

h1:=requal(0, 0, u1, 0, 0, 0, x1, x2);
h2:=midpoint(1, u1, 0, x3, x4, x1, x2);
h3:=midpoint(2, u1, 0, x3, x4, x1, x2);
h4:=vertically(0, 0, x3, x4, u1, 0, x1, x2);
h5:=collinear(u2, u3, x1, x2, 0, 0);
h6:=collinear(u1, 0, x3, x4, x1, x2);



% ---------------------------------------------------------------------;
% 直線BC上に、BA=BF となる点Fをとる -----------------------------------------;
% FAの中点をGとすると、△BFAは二等辺三角形だから、BG⊥FAとなる --------------------;
% FはBC上にあるのだから、共線となっている ( B-F-Cと考える ) -----------------------;
% また、GはFA上にあり、共線となっている ( F-G-Aと考える ) -------------------------;
% F(x5, x6) G(x7, x8)

h7:=requal(u1, 0, 0, 0, u1, 0, x5, x6);
h8:=midpoint(1, x5, x6, x7, x8, 0, 0);
h9:=midpoint(2, x5, x6, x7, x8, 0, 0);
h10:=vertically(u1, 0, x7, x8, x5, x6, 0, 0);
h11:=collinear(u1, 0, x5, x6, u2, u3);
h12:=collinear(x5, x6, x7, x8, 0, 0);


% 点IはAE上にあり、かつBG上にある (AEとBGの交点である) -------------------------;
% つまり、共線となっている ( A-I-E , B-I-G と考える)
% I(x10, x11)

h13:=collinear(0, 0, x10, x11, x3, x4);
h14:=collinear(u1, 0, x10, x11, x7, x8);




%----------------------------------------------------------------------;
% 結論 ----------------------------------------------------------------;
% 二角の二等分線が交わる点IはCH上にあり、共線となっている ( C-I-Hと考える ) --------;
% H(x9, 0) I(x10, x11)

i_conclusion:=collinear(u2, u3, x10, x11, x9, 0);

showtime;


%----------------------------------------------------------------------;
% Groebner Basis: 結果が1となったら、仮定が誤っている可能性が高い ---------------;

%%% 変数を定義し、lex形式で並べる ------------------------------------------;
torder({x11, x10, x9, x8, x7, x6, x5,x4, x3, x2, x1}, lex)$

%%% 仮定において定義した式からGroebner Basisを求める -------------------------;
gb:=groebner{h1, h2, h3, h4, h5, h6, h7, h8, h9, h10, h11, h12, h13, h14};

%---------------------------------------------------------;
% 「glterms」が出力するのは、グレブナー基底の計算過程で〈ゼロにはならない〉
% と仮定された式のリストである。
%---------------------------------------------------------;

%%% u に関する制約条件 --------------------------------------------------;
glterms;


%%% gbを法としてgを簡約 --------------------------------------------------;
preduce(i_conclusion, gb);

% ==> 0 になっていれば、定理は成立 ------------------------------------------;


%-------------------------------------------------------------;
% first(list), second(list)はそれぞれリスト内の1つ目、2つ目の要素を返す
% solve(f, v) は, 関数f の変数をv の方程式としてみて解く
%-------------------------------------------------------------;

% X座標も求めてみる ------------------------------------------------------;
%solve(first(gb), x8);
%solve(second(gb), x7);
%----------------------------------------------------------------------;

showtime;

;end;