% [定理 ２５] ----------------------------------------------------------;
% △ABCの頂点A,B,Cから直線gに降ろした垂線の足をD,E,Fとすると、
% 点D,E,FからそれぞれBC, CA, ABにおろした垂線DP,EQ,FRは一点で交わる
% A(u1, u2) B(0, 0) C(u3, 0)
% D(x1, x2) E(x3, x4) F(x5, x6)
% P(x7, x8) Q(x9, x10) R(x11, x12)
%----------------------------------------------------------------------;

% 関数定義読み込み） ----------------------------------------------------;

load_package groebner;
on comp,gcd,ezgcd;
off allfac,pwrds;

in cal_sys_relations$


% ２点間のユークリッド距離D^2 -----------------------------------------------;
procedure squared_euclid(a1,a2,b1,b2)$
begin
 scalar d;
 d:=(a1-b1)^2+(a2-b2)^2;
 return d
end$
%----------------------------------------------------------------------;


% ---------------------------------------------------------------------;
% ＜証明＞ -------------------------------------------------------------;

order x12, x11, x10, x9, x8, x7, x6, x5, x4, x3, x2, x1, u3, u2, u1;
factor x12, x11, x10, x9, x8, x7, x6, x5, x4, x3, x2, x1;

%----------------------------------------------------------------------;
% 仮定 ----------------------------------------------------------------;

% D-E-Fは共線となっている
h1:=collinear(x1, x2, x3, x4, x5, x6);

% AD⊥EF , BE⊥FD , CF⊥DE
h2:=vertically(u1, u2, x1, x2, x3, x4, x5, x6);
h3:=vertically(0, 0, x3, x4, x5, x6, x1, x2);
h4:=vertically(u3, 0, x5, x6, x1, x2, x3, x4);

% 点D,E,Fからそれぞれ辺BC,CA,ABに下ろした垂線の足をP,Q,Rとする
% DP⊥BC かつ B-P-C
h5:=vertically(x1, x2, x7, x8, 0, 0, u3, 0);
h6:=collinear(0, 0, x7, x8, u3, 0);

% EQ⊥CA かつ C-Q-A
h7:=vertically(x3, x4, x9, x10, u3, 0, u1, u2);
h8:=collinear(u3, 0, x9, x10, u1, u2);

% FR⊥AB かつ A-R-B
h9:=vertically(x5, x6, x11, x12, u1, u2, 0, 0);
h10:=collinear(u1, u2, x11, x12, 0, 0);

%----------------------------------------------------------------------;
% 結論 ----------------------------------------------------------------;

% DP, EQ, FRの共点を言うためには、§６例３により、
% DB^2 - DC^2 + EC^2 -EA^2 + FA^2 -FB^2 = 0 を示せれば良い

db2:=squared_euclid(x1, x2, 0, 0);
dc2:=squared_euclid(x1, x2, u3, 0);
ec2:=squared_euclid(x3, x4, u3, 0);
ea2:=squared_euclid(x3, x4, u1, u2);
fa2:=squared_euclid(x5, x6, u1, u2);
fb2:=squared_euclid(x5, x6, 0, 0);

conclusion:=db2-dc2+ec2-ea2+fa2-fb2;


%----------------------------------------------------------------------;
% Groebner Basis: 結果が1となったら、仮定が誤っている可能性が高い ---------------;

%%% 変数を定義し、lex形式で並べる ------------------------------------------;
torder({x12, x11, x10, x9, x8, x7, x6, x5, x4, x3, x2, x1}, lex)$

%%% 仮定において定義した式からGroebner Basisを求める -------------------------;
gb:=groebner{h1, h2, h3, h4, h5, h6, h7, h8, h9, h10};

%---------------------------------------------------------;
% 「glterms」が出力するのは、グレブナー基底の計算過程で〈ゼロにはならない〉
% と仮定された式のリストである。
%---------------------------------------------------------;

%%% u に関する制約条件 --------------------------------------------------;
glterms;


%%% gbを法としてgを簡約 --------------------------------------------------;
preduce(conclusion, gb);

% ==> 0 になっていれば、定理は成立 ------------------------------------------;

showtime;
;end;